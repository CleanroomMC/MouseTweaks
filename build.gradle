buildscript {
    repositories {
        maven {
            url = "https://maven.cleanroommc.com"
        }
        maven {
            url = "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}


apply plugin: 'net.minecraftforge.gradle.forge'

version = "3.1.2"
group = "com.cleanroommc"
archivesBaseName = "MouseTweaks"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    mappings = "stable_39"

    replace '@VERSION@', project.version
}

repositories {
    maven {
        url "https://cursemaven.com"
    }
}

dependencies {
    deobfCompile "curse.maven:jei-238222:4393461"
    compileOnly "curse.maven:projectE-226410:2702991"
    compileOnly "curse.maven:packagedauto-308380:4478952"
}

jar {
    from sourceSets.api.output
    archiveName = "${project.archivesBaseName}-${version}-mc${project.minecraft.version}.jar"
}

sourceJar {
    from sourceSets.api.allSource
    archiveName = "${project.archivesBaseName}-${version}-mc${project.minecraft.version}-src.jar"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    archiveName = "${project.archivesBaseName}-${version}-mc${project.minecraft.version}-api.jar"
}

artifacts {
    archives apiJar
}
